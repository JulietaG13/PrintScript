plugins {
    id 'base'
    id 'jacoco-report-aggregation'
}

repositories {
    mavenCentral()
}

dependencies {
    jacocoAggregation project(':lexer')
    jacocoAggregation project(':parser')
    jacocoAggregation project(':interpreter')
    jacocoAggregation project(':linter')
    jacocoAggregation project(':cli')
    jacocoAggregation project(':formatter')
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

tasks.register('jacocoAggregatedCoverageVerification', JacocoCoverageVerification) {
    executionData(fileTree(dir: "$buildDir/jacoco", include: "*.exec"))

    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

tasks.named('check') {
    dependsOn(tasks.named('testCodeCoverageReport'), tasks.named('jacocoAggregatedCoverageVerification'))
}